#created on: Apr 17, 2008
package NodeRules

#list any import classes here.
import uk.ac.lancs.comp.khatchad.rejuvenatepc.core.graph.*;
import ca.mcgill.cs.swevo.jayfx.model.*;

#declare any global variables here

# Two nodes are connected when there is an edge in between them. (base case).
rule "connected"
	when
		$edge : IntentionEdge()
	then
		Path<IntentionEdge<IElement>> aPath = new Path<IntentionEdge<IElement>>();
		aPath.push($edge);
		insert(aPath);
end

rule "path building"
	when
		$edge : IntentionEdge($source : fromNode, $target : toNode)
		$path : Path(size < 2, topNode == $target, tailNodes not contains $source) # more work here?
	then
		Path aPath = (Path)$path.clone();
		aPath.push($edge);
		insert(aPath);
end

query "all paths"
	$path : Path()
end

query "forward suggested execution nodes"
	$enabledNode : IntentionNode(enabled == true)
	$enabledPath : Path(firstNode == $enabledNode, $commonNode : lastNode, $enabledSize : size)
	$intersectingPath : Path($suggestedNode : firstNode, lastNode == $commonNode, size == $enabledSize) 
	eval($enabledPath.typeEquivalent($intersectingPath))
end

query "backward suggested execution nodes"
	$enabledNode : IntentionNode(enabled == true)
	$enabledPath : Path(lastNode == $enabledNode, $commonNode : firstNode, $enabledSize : size)
	$intersectingPath : Path($suggestedNode : lastNode, firstNode == $commonNode, size == $enabledSize) 
	eval($enabledPath.typeEquivalent($intersectingPath))
end

query "forward suggested X arcs" (Relation x)
	$enabledEdge : IntentionEdge(enabled == true, type == x)
	$enabledPath : Path(firstEdge == $enabledEdge, $commonNode : lastNode, $enabledSize : size)
	$intersectingPath : Path($suggestedEdge : firstEdge, lastNode == $commonNode, size == $enabledSize) 
	eval($enabledPath.typeEquivalent($intersectingPath))
end

query "backward suggested X arcs" (Relation x)
	$enabledEdge : IntentionEdge(enabled == true, type == x)
	$enabledPath : Path(lastEdge == $enabledEdge, $commonNode : firstNode, $enabledSize : size)
	$intersectingPath : Path($suggestedEdge : lastEdge, firstNode == $commonNode, size == $enabledSize) 
	eval($enabledPath.typeEquivalent($intersectingPath))
end
